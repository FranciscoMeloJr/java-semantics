// This program tests multi-dimensional arrays.

class Matrix {
  var lines:int, columns:int, matrix:arrayOf arrayOf int;

  method Matrix(x:int,y:int) {
    lines = x;
    columns = y;
    var a[x,y] : arrayOf arrayOf int;
    matrix = a;
  }

  method readMatrix() {
    for i = 0 to lines-1 do {
      print("Line ",i+1," (",columns," elements): ");
      for j = 0 to columns-1 do
        matrix[i][j] = read();
    }
  }

  method printMatrix() {
    for i = 0 to lines-1 do {
      print("Line ",i+1," (",columns," elements):");
      for j = 0 to columns-1 do
        print(" ",matrix[i,j]);
    print("\n");
    }
  }

  method copy() : class Matrix {
    var a : class Matrix = new Matrix(lines,columns);
    for i = 0 to lines - 1 do
      for j = 0 to columns - 1 do
        a.matrix[i,j] = matrix[i,j];
    return a;
  }

  method transpose() {
    var a[columns,lines] : arrayOf arrayOf int;
    for i = 0 to lines-1 do
      for j = 0 to columns-1 do
        a[j,i] = matrix[i,j];
    var temp:int = lines;
    lines = columns;
    columns = temp;
    matrix = a;
  }

  method multiply(a : class Matrix) {
    if a.lines != columns then {
      print("Incompatible dimensions.\n");
      return;
    }
    var b[lines,a.columns] : arrayOf arrayOf int;
    for i = 0 to lines-1 do
      for j = 0 to a.columns-1 do {
        b[i,j] = 0;
        for k = 0 to columns-1 do
          b[i,j] = b[i,j] + matrix[i,k] * a.matrix[k,j];
      }
    columns = a.columns;
    matrix = b;
  }
}


class main {
  method main() {
    print("Input the number of lines and columns (two natural numbers): ");
    var x:int = read(), y:int = read(), a : class Matrix = new Matrix(x,y);
    a.readMatrix();
    print("Your matrix is:\n");
    a.printMatrix();
    var b : class Matrix = a.copy();
    print("Here is a copy of your matrix:\n");
    b.printMatrix();
    print("The transpose of your matrix is:\n");
    b.transpose();
    b.printMatrix();
    print("You matrix multiplied with its transpose is:\n");
    var c : class Matrix = a.copy();
    c.multiply(b);
    c.printMatrix();
    print("The transpose of your matrix multiplied with your matrix is:\n");
    b.multiply(a);
    b.printMatrix();
    print("Done.\n");
  }
}

// Should output what it says.
